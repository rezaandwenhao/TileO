external ca.mcgill.ecse223.tileo.model;

namespace ca.mcgill.ecse223.tileo.controller;
class PlayController {
mode {
Ready {
startGame / {
doStartGame();
}  -> Roll; 
load(Game selectedGame) [isInGameMode(selectedGame)] / { 
doLoad(selectedGame);
}  -> Roll; 
load(Game selectedGame) [isInWonMode(selectedGame)] / {
doLoad(selectedGame);
}  -> Won; 
load(Game 
selectedGame) [isNotInGameOrWonMode(selectedGame)] / { 
doLoad(selectedGame);
}  -> ActionCard; 
} 
Roll {
rollDie() / {
possibleMoves = doRollDie();
}  -> Move; 
} 
Move {
land(Tile tile) [isNormalTile(tile)] / {
doLandTile(tile);
}  -> Roll; 
land(Tile tile) [isWinTile(tile)] / {
doLandTile(tile);
}  -> Won; 
land(Tile tile) [isActionTile(tile)] / {
doLandTile(tile);
}  -> ActionCard; 
} 
ActionCard {
playRollDieActionCard()
 [isRollDieActionCard()]
 / {  
possibleMoves = doPlayRollDieActionCard(); 
}  -> Roll; 
playConnectTilesActionCard(Tile tile1, Tile tile2)  
[isConnectTilesActionCard()]
 / {  
doPlayConnectTilesActionCard(tile1, tile2); 
}  -> Roll; 
playRemoveConnectionActionCard(Connection c)  
[isRemoveConnectionActionCard()]
 / {  
doPlayRemoveConnectionActionCard(c); 
}  -> Roll; 
playTeleportActionCard(Tile tile) [isTeleportAndNormalTile(tile)] / { 
doPlayTeleportActionCard(tile);
}  -> Roll; 
playTeleportActionCard(Tile tile) [isTeleportAndWinTile(tile)] / {
doPlayTeleportActionCard(tile);
}  -> Won; 
playTeleportActionCard(Tile tile) [isTeleportAndActionTile(tile)] / { 
doPlayTeleportActionCard(tile);
}  -> ActionCard; 
playLoseTurnActionCard()
[isLoseTurnActionCard()] 
/ { 
doPlayLoseTurnActionCard();
}  -> Roll; 
} 
} 
}
